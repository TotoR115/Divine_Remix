/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// implement sphere system                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Preparation		 			               	   \\\\\
/////                                                  \\\\\

/// kit modifications
INCLUDE ~divine_remix/lib/Divine_kit_sphere.tph~

// Spells and alignment
///////////// restrict gods specific spells to specific alignements

// Storm Shell (Talos)
ACTION_IF !(IDS_OF_SYMBOL ("spell" "CLERIC_STORM_SHELL") < 0) BEGIN
  OUTER_SET code=IDS_OF_SYMBOL ("spell" "CLERIC_STORM_SHELL") - 1000
  OUTER_SPRINT stormshell "SPpr%code%"
  COPY_EXISTING "%stormshell%.SPL" "override"
	WRITE_BYTE 0x1e 0b00000100 // excusion flags: good
END

COPY_EXISTING ~SPPR301.spl~ ~override~ // animate dead
  WRITE_BYTE 0x1e 0b00000100 // excusion flags: good

// Shield and greater Shield of Lathander
ACTION_IF !(IDS_OF_SYMBOL ("spell" "CLERIC_SHIELD_OF_LATHANDER") < 0) BEGIN
  OUTER_SET code=IDS_OF_SYMBOL ("spell" "CLERIC_SHIELD_OF_LATHANDER") - 1000
  OUTER_SPRINT lathander "SPpr%code%"
END ELSE OUTER_SPRINT lathander ""
ACTION_IF !(IDS_OF_SYMBOL ("spell" "CLERIC_GREATER_SHIELD_OF_LATHANDER") < 0) BEGIN
  OUTER_SET code=IDS_OF_SYMBOL ("spell" "CLERIC_GREATER_SHIELD_OF_LATHANDER") - 1000
  OUTER_SPRINT glathander "SPpr%code%"
END ELSE OUTER_SPRINT glathander ""
COPY_EXISTING ~%lathander%.SPL~ ~override~
			  ~%glathander%.SPL~ ~override~
  WRITE_BYTE 0x1e 0b00000010 // excusion flags: evil
BUT_ONLY IF_EXISTS

// Moon spells from SelÃ»ne
ACTION_IF !(IDS_OF_SYMBOL ("spell" "CLERIC_MOONBLADE") < 0) BEGIN
  OUTER_SET code=IDS_OF_SYMBOL ("spell" "CLERIC_MOONBLADE") - 1000
  OUTER_SPRINT moonbl "SPpr%code%"
END ELSE OUTER_SPRINT moonbl ""
ACTION_IF !(IDS_OF_SYMBOL ("spell" "CLERIC_WALL_OF_MOONLIGHT") < 0) BEGIN
  OUTER_SET code=IDS_OF_SYMBOL ("spell" "CLERIC_WALL_OF_MOONLIGHT") - 1000
  OUTER_SPRINT moonwall "SPpr%code%"
END ELSE OUTER_SPRINT moonwall ""
COPY_EXISTING ~%moonbl%.SPL~ ~override~
			  ~%moonwall%.SPL~ ~override~
  WRITE_BYTE 0x1e 0b00010010 // excusion flags: evil
BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// create sphere framework                          \\\\\
/////                                                  \\\\\

COPY ~divine_remix/spheres/hla_blank.2da~ ~divine_remix/spheres/hla_master.2da~

ACTION_FOR_EACH sphere IN al an as cs cm co cr di ex ea ee ef ew gu he hr lw ne nr nu pl pr su sn sm th ti tr wa wd we BEGIN

  ACTION_PHP_EACH cd_table_lookups AS class => table BEGIN
  
    COPY ~divine_remix/spheres/%class%.2da~ ~divine_remix/spheres/%class%_%sphere%_major.2da~
      REPLACE_TEXTUALLY ~CDREPLACE\([1-7]\)~ ~AP_CDDR%sphere%\1~
  
    COPY ~divine_remix/spheres/%class%_%sphere%_major.2da~ ~divine_remix/spheres/%class%_%sphere%_minor.2da~
      REPLACE_TEXTUALLY ~AP_CDDR%sphere%[4-7]~ ~****~ // delete levels 4-7 for minor access
                 
    COPY ~divine_remix/spheres/hla_blank.2da~ ~divine_remix/spheres/hla_%sphere%.2da~

    OUTER_FOR (index = 1 ; index < 8 ; ++index) BEGIN

      COPY ~divine_remix/spheres/template.spl~           ~override/cddr%sphere%%index%.spl~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_all.2da~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_evil.2da~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_good.2da~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_chaotic.2da~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_lawful.2da~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_geneutral.2da~
           ~divine_remix/spheres/spellbook_template.2da~ ~override/spellbook_%sphere%_%index%_lcneutral.2da~
    
    END
  
  END

END

/////                                                  \\\\\
///// assign spells to sphere spells                   \\\\\
/////                                                  \\\\\

ACTION_IF !MOD_IS_INSTALLED ~SPELL_REV.TP2~ ~0~ BEGIN
  INCLUDE ~divine_remix/lib/dr_sphere_spell.tph~ 
END ELSE INCLUDE ~divine_remix/lib/dr_sphere_spell_rev.tph~

/////                                                  \\\\\
///// generic & custom sphere assignments              \\\\\
/////                                                  \\\\\


ACTION_IF !MOD_IS_INSTALLED ~SPELL_REV.TP2~ ~0~ BEGIN
  INCLUDE ~divine_remix/lib/sphere_assignment.tph~ 
END ELSE INCLUDE ~divine_remix/lib/sphere_assignment_spell_rev.tph~ 

/////                                                  \\\\\
///// special: cleric-rangers                          \\\\\
/////                                                  \\\\\

// merge spell lists. Lazy as f***, oh yes.
ACTION_FOR_EACH align IN 0 17 18 19 33 34 35 49 50 51 BEGIN

  OUTER_FOR (index = 1 ; index < 8 ; ++index) BEGIN //

    COPY ~divine_remix/spheres/spellbook_clabpr01_%index%_%align%.2da~ ~divine_remix/spheres/spellbook_clericranger_%index%_%align%.2da~
      LAUNCH_PATCH_MACRO ~remove_blank_lines_from_eof~ // remove eof blank lines
      APPEND_FILE ~divine_remix/spheres/spellbook_clabpr01_%index%_%align%.2da~
  
  END

END

/////                                                  \\\\\
/////   spellbooks update	                           \\\\\
/////                                                  \\\\\

ACTION_CLEAR_ARRAY cd_cleric_kits
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN

  19 => clabpr02
  20 => clabpr03
  21 => clabpr04

END

ACTION_CLEAR_ARRAY cd_cleric_classes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_cleric_classes BEGIN

    3 => clabpr01     // cleric
    6 => clabpa01     // paladin
    8 => clabpr01     // figher-cleric
   11 => clabdr01     // druid
   12 => clabrn01     // ranger
   14 => clabpr01     // cleric-mage
   15 => clabpr01     // cleric-thief
   16 => clabdr01     // fighter-druid
   17 => clabpr01     // figher-mage-cleric
   18 => clericranger // cleric-ranger
  204 => clabpr01     // cleric_all
  207 => clabpa01     // paladin_all
  208 => clabdr01     // druid_all
  209 => clabrn01     // ranger_all

END

// find new kits in kitlist
COPY_EXISTING ~kitlist.2da~ ~override~
  SET "A#CYRIC" = 0
  SET "A#FEYW" = 0
  SET "A#KOSS" = 0
  SET "A#OGMA" = 0
  SET "A#RED" = 0
  SET "A#TEMP" = 0
  SET "CDILMATR" = 0
  SET "CDSELUNE" = 0
  SET "CDXVIM" = 0
  SET "NMSUNE" = 0
  SET "OHTYR" = 0
  SET "A#OOZE" = 0
  COUNT_2DA_ROWS 9 rows
  FOR (index = rows ; index > 31 ; --index) BEGIN
    READ_2DA_ENTRY index 5 9 clab
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#OOZE" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#OOZE"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#OOZE%" => "%clab%" END
    END
	PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#SHAR" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#SHAR"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#SHAR%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "CDSelune" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 CDSelune
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%CDSELUNE%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#RED" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#RED"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#RED%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "OHTYR" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 OHTYR
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%OHTYR%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#TEMP" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#TEMP"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#TEMP%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "OHTEMPUS" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#TEMP"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#TEMP%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "CDILMATR" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 CDILMATR
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%CDILMATR%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#KOSS" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#KOSS"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#KOSS%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "CDXvim" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 CDXvim
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%CDXVIM%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#OGMA" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#OGMA"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#OGMA%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "NMSUNE" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 NMSUNE
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%NMSUNE%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "a#feyw" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "a#feyw"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#FEYW%" => "%clab%" END
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "A#CYRIC" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 "A#CYRIC"
      DEFINE_ASSOCIATIVE_ARRAY cd_cleric_kits BEGIN "%A#CYRIC%" => "%clab%" END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// adjusting spellbooks is long ang ugly, so moved grunt work to external macro libraries
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  SET kit = 0
  SET sb_delta = 0
  READ_SHORT   0x244 kit1
  READ_BYTE    0x246 kit2
  READ_BYTE    0x247 kit3
  PATCH_IF ((kit1 = 0) AND (kit3 = 0x40)) BEGIN
    PATCH_PHP_EACH cd_cleric_kits AS kitno => clabcheck BEGIN
      PATCH_IF ((IS_AN_INT kitno) AND (kitno = kit2)) BEGIN
        SET kit = 1
        LPF cd_spellbook STR_VAR clab = EVAL "%clabcheck%" END
      END
    END
  END
  PATCH_IF kit = 0 BEGIN // if not picked up by kit check
    READ_BYTE 0x273 "class" ELSE 0
    PATCH_PHP_EACH cd_cleric_classes AS classno => classclab BEGIN
      PATCH_IF (classno = class) BEGIN
        LPF cd_spellbook STR_VAR clab = EVAL "%classclab%" END
      END
    END
  END
  //LPF cd_spellbook_wrapup END
  BUT_ONLY

/////                                                  \\\\\
///// hla stuff                                        \\\\\
/////                                                  \\\\\

ACTION_IF GAME_IS ~tob bg2ee eet bgt~ THEN BEGIN // hlas

  COPY_EXISTING ~spcl900.spl~ ~override/cdprwrl.spl~ // hla whirlwind
                ~spcl906.spl~ ~override/cdprpwr.spl~ // hla power attack
                ~spcl907.spl~ ~override/cdprhdy.spl~ // hla hardiness
                ~spcl917.spl~ ~override/cdpravd.spl~ // hla avoid death
                ~spcl902.spl~ ~override/cdprdbl.spl~ // hla death blow
                ~spcl908.spl~ ~override/cdprcry.spl~ // hla war cry
                ~spcl922.spl~ ~override/cdprtrk.spl~ // hla tracking
    LAUNCH_PATCH_MACRO cd_hla_descript

  COPY_EXISTING ~spcl913.spl~ ~override/cdprevd.spl~ // hla evasion
    SAY UNIDENTIFIED_DESC @10176

  COPY_EXISTING ~spcl904.spl~ ~override/cdprrmg.spl~  // resist magic
    SAY UNIDENTIFIED_DESC @177

  COPY ~Divine_Remix/spl/cdlthla.spl~ ~override~ // Lathander's Renewal
    SAY NAME1 @10192
    SAY NAME2 @10192
    SAY UNIDENTIFIED_DESC @10193
    SAY DESC @10193

  COPY ~Divine_Remix/spl/cdprscr.spl~ ~override~ // scribe scrolls
    SAY NAME1 #70282
    SAY NAME2 #70282
    SAY UNIDENTIFIED_DESC @10102
    SAY DESC @10102

  // assign kits unique hla tables
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_sphere_hla_assign BEGIN
  // add druid, ranger, paladin tables
    ~a#cyric~     => ~a#cyri~
    ~a#FEYWARDEN~ => ~a#feyw~
    ~a#koss~      => ~a#koss~
    ~a#ogma~      => ~a#ogma~
    ~a#ooze~      => ~a#ooze~
    ~a#red~       => ~a#red~
    ~a#shar~      => ~a#shar~
    ~a#temp~      => ~a#temp~
    ~cdilmatr~    => ~cdilma~
    ~cdselune~    => ~cdselu~
    ~cdxvim~      => ~cdxvim~
    ~nmsune~      => ~nmsune~
    ~talos~       => ~cdtalo~
    ~helm~        => ~cdhelm~
    ~lathander~   => ~cdlath~
    ~ohtyr~       => ~cdtyr~
    ~ohtempus~    => ~a#temp~
    ~CLERIC~      => ~cdcler~
  END

  COPY_EXISTING ~luabbr.2da~ ~override~
    PATCH_PHP_EACH cd_sphere_hla_assign AS kit => table BEGIN
      REPLACE_TEXTUALLY ~^\(%kit%[ %TAB%]+\)[^ ^%TAB%]+[ %TAB%]*$~ ~\1%table%~
    END

  // patch hla table to exlude quest-level spells from disallowed spheres
  ACTION_FOR_EACH table IN lucr0 BEGIN // cleric-ranger; patch separate since two potential sources of allowed hlas
  
    COPY_EXISTING ~%table%.2da~ ~override~
      LAUNCH_PATCH_MACRO ~remove_blank_lines_from_eof~ // remove blank lines at end of file
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%][0-9]+[ %TAB%]+\*[ %TAB%]+.+$~ ~~ // delete empty ability lines
      // delete quest spells not available
      REPLACE_EVALUATE ~\([%LNL%%MNL%%WNL%][^ ^%TAB%]+[ %TAB%]+GA_SPPR\)\([0-9][0-9][0-9]\)\([ %TAB%]+[^ ^%TAB%]+[ %TAB%]+[^ ^%TAB%]+[ %TAB%]+[^ ^%TAB%]+[ %TAB%]+[^ ^%TAB%]+[ %TAB%]+AP_CDDR\)\([0-9][0-9][0-9]\)\([ %TAB%]+[^ ^%TAB%]+[ %TAB%]+[^ ^%TAB%]+\)~ BEGIN
        // if clab lakes AP_spell, delete line
        PATCH_IF foo=1 BEGIN //((MATCH2 = MATCH4) AND (!FILE_CONTAINS_EVALUATED (~clabpr01.2da~ ~[ %TAB%]+AP_CDDR[0-9][0-9][0-9][ %TAB%]+~)) AND (!FILE_CONTAINS_EVALUATED (~clabrn01.2da~ ~[ %TAB%]+AP_CDDR[0-9][0-9][0-9][ %TAB%]+~))) BEGIN
          SPRINT replace ""
        END ELSE BEGIN
          SPRINT replace "%MATCH1%%MATCH2%%MATCH3%%MATCH4%%MATCH5%"
        END
      END ~%replace%~
      PATCH_IF FILE_EXISTS ~divine_remix/2da/hla_%table%.2da~ BEGIN
        APPEND_FILE ~divine_remix/2da/hla_%table%.2da~
      END
      APPEND_FILE ~divine_remix/spheres/hla_padding.2da~
      COUNT_2DA_ROWS 10 rows
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        PATCH_IF (index < 24) BEGIN
          SET_2DA_ENTRY index 0 10 (index + 1) // re-number rows
        END ELSE BEGIN
          SET_2DA_ENTRY index 0 10 DELETE_ROW
        END
      END
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]DELETE_ROW+.+$~ ~~ // delete lines past 24
      REPLACE_TEXTUALLY ~AP_CDDR[0-9][0-9][0-9]~ ~*         ~ // delete temp placeholders
      PRETTY_PRINT_2DA
      BUT_ONLY

  END
  
  ACTION_IF FILE_EXISTS_IN_GAME ~a#ooze.2da~ THEN BEGIN
    COPY_EXISTING ~ludr0.2da~ ~override/lua#ooze.2da~
  END

  // patch hla table to exlude quest-level spells from disallowed spheres
  ACTION_FOR_EACH class IN ~ohtempus~ ~a#temp~ BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%class%.2da~ THEN BEGIN
	  OUTER_TEXT_SPRINT "tempus" "%class%"
	END
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_sphere_hla_match BEGIN
  
    ~a#cyri~ => ~a#cyric~
    ~a#feyw~ => ~a#feyw~
    ~a#koss~ => ~a#koss~
    ~a#ogma~ => ~a#ogma~
    ~a#ooze~ => ~a#ooze~
    ~a#red~  => ~a#red~
    ~a#shar~ => ~a#shar~
    ~a#temp~ => ~%tempus%~
    ~cdilma~ => ~cdilmatr~
    ~cdselu~ => ~cdselune~
    ~cdxvim~ => ~cdxvim~
    ~nmsune~ => ~nmsune~
    ~cdtalo~ => ~clabpr02~
    ~cdhelm~ => ~clabpr03~
    ~cdlath~ => ~clabpr04~
    ~cdtyr~  => ~ohtyr~
    ~cm0~    => ~clabpr01~ // cleric-mages
    ~ct0~    => ~clabpr01~ // cleric-thief
    ~dr0~    => ~clabdr01~ // druid
    ~fc0~    => ~clabpr01~ // fighter-cleric
    ~fd0~    => ~clabdr01~ // fighter-druid
    ~fmc~    => ~clabpr01~ // fighter-mage-cleric
//    ~cr0~    => ~clabpr01~ // cleric-ranger; special, handled above
    ~cdcler~    => ~clabpr01~ // keep last as its used as template for others

  END
  
  ACTION_PHP_EACH cd_sphere_hla_match AS table => clab BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%clab%.2da~ THEN BEGIN
      
      ACTION_IF FILE_EXISTS_IN_GAME ~lu%table%.2da~ THEN BEGIN

        OUTER_SPRINT source_table ~lu%table%~

      END ELSE BEGIN

        OUTER_SPRINT source_table ~lucl0~

      END

      COPY_EXISTING ~%source_table%.2da~ ~override/lu%table%.2da~
        LAUNCH_PATCH_MACRO ~remove_blank_lines_from_eof~ // remove blank lines at end of file
        REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%][0-9]+[ %TAB%]+\*[ %TAB%]+.+$~ ~~ // delete empty ability lines
        REPLACE_TEXTUALLY ~%TAB%~ ~ ~
        // delete quest spells not available
        REPLACE_EVALUATE ~\([%LNL%%MNL%%WNL%][0-9]+ +GA_SPPR\)\([1-7][0-9][0-9]\)\( .+\)\([%LNL%%MNL%%WNL%][0-9]+ +GA_SPCL\)\([1-7][0-9][0-9]\)\( .+\)$~ BEGIN
          // if clab lakes AP_spell, delete line
          PATCH_IF !FILE_CONTAINS_EVALUATED (~%clab%.2da~ ~CD_HLA%MATCH2%[ %TAB%]+~) BEGIN
            SPRINT replace ""
          END ELSE BEGIN
            SPRINT replace "%MATCH1%%MATCH2%%MATCH3%"
          END
        END ~%replace%~

      // brief interlude to add allowed hlas from master list not already in table
      COPY_EXISTING ~divine_remix/spheres/hla_master.2da~ ~divine_remix/spheres/hla_master.2da~
        COUNT_2DA_ROWS 10 rows
        FOR (index = 0 ; index < rows ; ++index) BEGIN
          READ_2DA_ENTRY index 1 10 ABILITY
          INNER_PATCH_SAVE ABIL_SHORT ~%ABILITY%~ BEGIN
            REPLACE_TEXTUALLY ~GA_SPCL~ ~CD_HLA~
			REPLACE_TEXTUALLY ~GA_SPPR~ ~CD_HLA~
          END
          PATCH_IF ((!FILE_CONTAINS_EVALUATED (~override/lu%table%.2da~ ~%ABILITY%~)) AND // hla on master table that's not in the current hla table
                     (FILE_CONTAINS_EVALUATED (~%clab%.2da~ ~%ABIL_SHORT%[ %TAB%]+~))) BEGIN // and is allowed per the clab
            READ_2DA_ENTRY index 2 10 ICON
            READ_2DA_ENTRY index 3 10 STRREF
            READ_2DA_ENTRY index 4 10 MIN_LEV
            READ_2DA_ENTRY index 5 10 MAX_LEVEL
            READ_2DA_ENTRY index 6 10 NUM_ALLOWED
            READ_2DA_ENTRY index 7 10 PREREQUISITE
            READ_2DA_ENTRY index 8 10 EXCLUDED_BY
            READ_2DA_ENTRY index 9 10 ALIGNMENT_RESTRICT
            INNER_ACTION BEGIN

              COPY ~override/lu%table%.2da~ ~override/lu%table%.2da~
                APPEND_FILE ~divine_remix/spheres/hla_append.2da~ EVALUATE_BUFFER

            END
          END
        END
        BUT_ONLY

      COPY_EXISTING ~override/lu%table%.2da~ ~override/lu%table%.2da~
        PATCH_IF FILE_EXISTS ~divine_remix/2da/hla_%table%.2da~ BEGIN
          APPEND_FILE ~divine_remix/2da/hla_%table%.2da~
        END
        APPEND_FILE ~divine_remix/spheres/hla_padding.2da~
        COUNT_2DA_ROWS 10 rows
        FOR (index = 0 ; index < rows ; ++index) BEGIN
          PATCH_IF (index < 24) BEGIN
            SET_2DA_ENTRY index 0 10 (index + 1) // re-number rows
          END ELSE BEGIN
            SET_2DA_ENTRY index 0 10 DELETE_ROW
          END
        END
        REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]DELETE_ROW+.+$~ ~~ // delete lines past 24
        REPLACE_TEXTUALLY ~AP_CDDR[0-9][0-9][0-9]~ ~*         ~ // delete temp placeholders
        PRETTY_PRINT_2DA
        BUT_ONLY

    END
    
    ACTION_IF FILE_EXISTS_IN_GAME ~%clab%.2da~ THEN BEGIN

      // clean up generic class tables
      COPY_EXISTING ~%clab%.2da~ ~override~
        REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]CD_HLA[0-9][0-9][0-9][ %TAB%]+.+$~ ~~ // delete temp placeholders
        BUT_ONLY

    END

  END

END