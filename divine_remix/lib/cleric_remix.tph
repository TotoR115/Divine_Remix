/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cleric Remix                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

COPY ~divine_remix/2da/mxsplprs.2da~ ~override~

/////                                                  \\\\\
///// universal elements for kit priests               \\\\\
/////                                                  \\\\\

// only if alignments not already shifted
ACTION_IF NOT FILE_CONTAINS_EVALUATED (~alignment.2da~ ~^LATHANDER[ %TAB%]+1[ %TAB%]+0[ %TAB%]+0[ %TAB%]+1[ %TAB%]+1[ %TAB%]+0[ %TAB%]+1[ %TAB%]+0[ %TAB%]+0~) THEN BEGIN

  COPY_EXISTING ~alignmnt.2da~ ~override~
    REPLACE_TEXTUALLY ~^TALOS[ %TAB%]+.+~     ~TALOS     0 0 0 0 0 1 0 1 1~
    REPLACE_TEXTUALLY ~^HELM[ %TAB%]+.+~      ~HELM      1 1 1 0 1 0 0 0 0~
    REPLACE_TEXTUALLY ~^LATHANDER[ %TAB%]+.+~ ~LATHANDER 1 0 0 1 1 0 1 0 0~
    PRETTY_PRINT_2DA
    BUT_ONLY

  ACTION_IF GAME_IS ~soa bg2 tob bg2ee eet bgt~ THEN BEGIN // temple stronghold stuff

    COMPILE ~divine_remix/dlg/temple.d~

    EXTEND_TOP ~ar0900.bcs~   ~divine_remix/baf/ar0900.baf~
    EXTEND_TOP ~ar0901.bcs~   ~divine_remix/baf/ar0901.baf~
    EXTEND_TOP ~ar0902.bcs~   ~divine_remix/baf/ar0902.baf~
    EXTEND_TOP ~ar0904.bcs~   ~divine_remix/baf/ar0904.baf~

  END

END

// new descripts
COPY_EXISTING ~kitlist.2da~ ~override~ // look for kit descripts
  SPRINT mixed_match @10154 // Priest
  SPRINT lower_match @10134 // priest
  SPRINT upper_match @10135 // PRIEST
  COUNT_2DA_ROWS 9 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 5 8 clab
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "clabpr02" = 0) BEGIN // talos
      SPRINT mixed_replace @10136 // Stormlord
      SPRINT lower_replace @10137 // stormlord
      SPRINT upper_replace @10138 // STORMLORD
      LAUNCH_PATCH_MACRO cd_descript_replace
    END
    PATCH_IF  ("%clab%" STRING_COMPARE_CASE "clabpr03" = 0) BEGIN // helm
      SPRINT mixed_replace @10139 // Watcher
      SPRINT lower_replace @10140 // watcher
      SPRINT upper_replace @10141 // WATCHER
      LAUNCH_PATCH_MACRO cd_descript_replace
    END
    PATCH_IF  ("%clab%" STRING_COMPARE_CASE "clabpr04" = 0) BEGIN // lathander
      SPRINT mixed_replace @10142 // Morninglord
      SPRINT lower_replace @10143 // morninglord
      SPRINT upper_replace @10144 // MORNINGLORD
      LAUNCH_PATCH_MACRO cd_descript_replace
    END
    PATCH_IF  ("%clab%" STRING_COMPARE_CASE "ohtempus" = 0) BEGIN // tempus
      SPRINT mixed_replace @10145 // Battleguard
      SPRINT lower_replace @10146 // battleguard
      SPRINT upper_replace @10147 // BATTLEGUARD
      LAUNCH_PATCH_MACRO cd_descript_replace
    END
    PATCH_IF  ("%clab%" STRING_COMPARE_CASE "ohtyr" = 0) BEGIN // tyr
      SPRINT mixed_replace @10148 // Holy Justice
      SPRINT lower_replace @10149 // holy justice
      SPRINT upper_replace @10150 // HOLY JUSTICE
      LAUNCH_PATCH_MACRO cd_descript_replace
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// Changes to HLAs and holy symbols for ToB         \\\\\
/////                                                  \\\\\

ACTION_IF GAME_IS ~bgt tob bg2ee eet~ THEN BEGIN // holy symbols

  COPY ~divine_remix/spl/CDHLYSYM.spl~ ~override~
       ~divine_remix/spl/CDHLYSY2.spl~ ~override~
       ~divine_remix/itm/CDHLYSYM.itm~ ~override~

  COPY_EXISTING ~clabpr02.2da~ ~override~
                ~clabpr03.2da~ ~override~
                ~clabpr04.2da~ ~override~
    REPLACE_TEXTUALLY ~AP_SPCl93[123] ~ ~AP_CDHLYSYM~
    BUT_ONLY_IF_IT_CHANGES
  
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_holy_symbol BEGIN
    GODHELM      => BELT13
    GODLATHANDER => BELT12
    GODTALOS     => BELT14
  END
  
  ACTION_PHP_EACH cd_holy_symbol AS kit => symbol BEGIN
  
    OUTER_FOR (index = 1 ; index < 7 ; ++index) BEGIN
  
      EXTEND_TOP ~baldur.bcs~   ~divine_remix/baf/holysym.baf~ EVALUATE_BUFFER
      EXTEND_TOP ~baldur25.bcs~ ~divine_remix/baf/holysym.baf~ EVALUATE_BUFFER
  
    END

  END

  COPY_EXISTING ~belt12.itm~ ~override~ // Lathander Holy symbol
    SAY UNIDENTIFIED_DESC @10183
    SAY DESC @10183
    READ_BYTE  0x1E "use1"
    WRITE_BYTE 0x1E ("%use1%" BAND 0b11110111) // unusable by evil alignments

  COPY_EXISTING ~belt13.itm~ ~override~ // Helm Holy Symbol
    SAY UNIDENTIFIED_DESC @10187
    SAY DESC @10187
    READ_BYTE  0x1E "use1"
    WRITE_BYTE 0x1E (("%use1%" BAND 0b11111001) BOR 0b00000001) // unusable by chaotic alignments

  COPY_EXISTING ~belt14.itm~ ~override~ // Talos holy symbol
    SAY UNIDENTIFIED_DESC @10182
    SAY DESC @10182
    READ_BYTE  0x1E "use1"
    WRITE_BYTE 0x1E (("%use1%" BAND 0b11110111) BOR 0b00010000) // unusable by lawful and good alignments

  OUTER_FOR (index = 1 ; index < 7 ; ++index) BEGIN // Change the way the script works to avoid usable issues with holy symbols. It is mostly for Chaotic neutral and lawful evil clerics - BG2 fixpack is needed.
  COPY_EXISTING ~baldur.bcs~ 	override
				~baldur25.bcs~	override
	  DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
		~Alignment(Player%index%,MASK_GENEUTRAL)~ 
		~OR(2)
	  Alignment(Player%index%,MASK_GENEUTRAL)
	  Alignment(Player%index%,MASK_LAWFUL)
	!Alignment(Player%index%,MASK_GOOD)
	!Alignment(Player%index%,MASK_CHAOTIC)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
		~Alignment(Player%index%,MASK_EVIL)~
		~OR(2)
	  Alignment(Player%index%,MASK_EVIL)
	  Alignment(Player%index%,MASK_CHAOTIC)
	!Alignment(Player%index%,MASK_GOOD)
	!Alignment(Player%index%,MASK_LAWFUL)~ // idem
	  END	
    BUT_ONLY
  END

END

/////                                                  \\\\\
///// misc                                             \\\\\
/////                                                  \\\\\

// added at Idobek's request for NPC Kit compatibility
ACTION_IF FILE_EXISTS_IN_GAME ~ikitano1.spl~ THEN BEGIN

  COPY_EXISTING ~ikitano1.spl~ ~override~
    SAY 0x9e @10101

END

/////                                                  \\\\\
///// assign kits                                      \\\\\
/////                                                  \\\\\

// find tempus, tyr kit in kitlist
COPY_EXISTING ~kitlist.2da~ ~override~
  SET ohtyr    = 0
  SET ~a#temp~ = 0
  COUNT_2DA_ROWS ~9~ "rows"
  FOR (index = rows ; index > 31 ; --index) BEGIN
    READ_2DA_ENTRY index 5 9 clab
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "OHTEMPUS" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 ~a#temp~
    END
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "OHTYR" = 0) BEGIN
      READ_2DA_ENTRY index 0 9 ohtyr
    END
  END
  BUT_ONLY_IF_IT_CHANGES

OUTER_SET kit = 19 // talos
ACTION_FOR_EACH creature IN acolyte1 bhnalla c6cler3 cldad cltalp01 ltalos scyarryl talmiss talmiss2 talvilon vvpriest yssold16 BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN
	
    COPY_EXISTING ~%creature%.cre~ ~override~ 
      WRITE_SHORT   0x244 0
      WRITE_SHORT   0x246 0x4000 + "%kit%"
//    WRITE_BYTE    0x247 0x40
	  READ_BYTE 0x234 "Lv"
	  READ_SHORT NAME1 "priestG"
	  GET_STRREF priestG "name" 
	  PATCH_IF ((("%name%" STRING_COMPARE_CASE "Priest of Talos" = 0) OR ("%name%" STRING_COMPARE_CASE "Cleric of Talos" = 0)) AND ("%Lv%">=5)) BEGIN
	   SAY NAME1 @10188 //Stormlord of Talos
	   SAY NAME2 @10188
	  END
	  BUT_ONLY
	  
  END

END

// Stormherald Nallabir

ACTION_IF MOD_IS_INSTALLED "song_and_silence.tp2" 3 BEGIN
    COPY_EXISTING ~nalla.cre~ ~override~ 
      WRITE_LONG   0x244 (IDS_OF_SYMBOL (~KIT~ ~A!CHORISTER~))
	  BUT_ONLY IF_EXISTS
END


OUTER_SET kit = 20 // helm
ACTION_FOR_EACH creature IN acolyte3 bhelm bhoisig c6cler2 helmbyr helmpr oisig sctelwyn nalin shugod01 shugod02 shugar01 ntprihel s#helm01 BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN

    COPY_EXISTING ~%creature%.cre~ ~override~  
      WRITE_SHORT   0x244 0
      WRITE_SHORT   0x246 0x4000 + "%kit%"
//    WRITE_BYTE    0x247 0x40
	  READ_SHORT NAME1 "priestG"
	  READ_BYTE 0x234 "Lv"
	  GET_STRREF priestG "name" 
	  PATCH_IF ((("%name%" STRING_COMPARE_CASE "Priest of Helm" = 0) OR  ("%name%" STRING_COMPARE_CASE "Cleric of Helm" = 0) OR  ("%name%" STRING_COMPARE_CASE "Priestess of Helm" = 0)) AND ("%Lv%">=5)) BEGIN
	  SAY NAME1 @10189 //Watcher of Helm
	  SAY NAME2 @10189
	  END
      BUT_ONLY

  END

END

OUTER_SET kit = 21 // lathander
ACTION_FOR_EACH creature IN acolyte2 amtcle01 amtcler0 arenthis arval ~b!gavin~ bharval blane bran c6cler1 cllatp01 cscleric dawnmas dsblane dsjetla dsjetla2 dsjetla4 dsjetla6 latlara scsain keldda BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN

    COPY_EXISTING ~%creature%.cre~ ~override~  
      WRITE_SHORT   0x244 0
      WRITE_SHORT   0x246 0x4000 + "%kit%"
//    WRITE_BYTE    0x247 0x40
	  READ_SHORT NAME1 "priestG"
	  READ_BYTE 0x234 "Lv"
	  GET_STRREF priestG "name" 
	  PATCH_IF ((("%name%" STRING_COMPARE_CASE "Priest of Lathander" = 0) OR  ("%name%" STRING_COMPARE_CASE "Cleric of Lathander" = 0)) AND ("%Lv%">=5)) BEGIN
	  SAY NAME1 @10190 //Morninglord of Lathander
	  SAY NAME2 @10190
	  END
      BUT_ONLY

  END

END

ACTION_IF ohtyr != 0 THEN BEGIN

  OUTER_SET kit = "%ohtyr%" // tyr
  ACTION_FOR_EACH creature IN ohdcru01 ohdcru02 ohdlufc1 ohdlumc1 ohdneo03 ohdneo04 ohdppe01 ohdppe02 ohdrdef ohdtree1 BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN

      COPY_EXISTING ~%creature%.cre~ ~override~
        WRITE_SHORT   0x244 0
        WRITE_SHORT   0x246 0x4000 + "%kit%"
  //    WRITE_BYTE    0x247 0x40
        BUT_ONLY

    END

  END

END

ACTION_IF "%a#temp%" != 0 THEN BEGIN

  OUTER_SET kit = "%a#temp%" // tempus
  ACTION_FOR_EACH creature IN kpchap01 everard accalia BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN

      COPY_EXISTING ~%creature%.cre~ ~override~
        WRITE_SHORT   0x244 0
        WRITE_SHORT   0x246 0x4000 + "%kit%"
  //    WRITE_BYTE    0x247 0x40
	    READ_SHORT NAME1 "priestG"
	    GET_STRREF priestG "name" 
	    PATCH_IF ("%name%" STRING_COMPARE_CASE "Priest of Tempus" = 0) OR  ("%name%" STRING_COMPARE_CASE "Cleric of Tempus" = 0) OR  ("%name%" STRING_COMPARE_CASE "Priestess of Tempus" = 0) BEGIN
	      SAY NAME1 @10191 //Battleguard of Tempus 
	      SAY NAME2 @10191
	    END        
		BUT_ONLY

    END

  END

END