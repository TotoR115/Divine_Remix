COPY ~divine_remix/2da/cdselune.2da~ ~override~

COPY ~Divine_Remix/spl/cdslmce.spl~ ~override~  // moon's hand (innate)
  SAY NAME1 @10303
  SAY NAME2 @10303
  SAY UNIDENTIFIED_DESC @10304
  SAY DESC @10304

COPY ~Divine_Remix/bam/cdislune.bam~ ~override~
COPY ~Divine_Remix/itm/cdslmc0.itm~ ~override~  // Moon Mace +0
  SAY NAME1 @10303
  SAY NAME2 @10303
  SAY UNIDENTIFIED_DESC @10304
  SAY DESC @10304

  SET "weap_idx" = 0

COPY_EXISTING ~cdslmc0.itm~ ~override/cdslmc1.itm~  // moon mace +1
              ~cdslmc0.itm~ ~override/cdslmc2.itm~  // moon mace +2
              ~cdslmc0.itm~ ~override/cdslmc3.itm~  // moon mace +3
              ~cdslmc0.itm~ ~override/cdslmc4.itm~  // moon mace +4
              ~cdslmc0.itm~ ~override/cdslmc5.itm~  // moon mace +5
  SET "weap_idx" = ("%weap_idx%" + 1)
  WRITE_LONG  0x60 "%weap_idx%"
  WRITE_SHORT 0x86 "%weap_idx%"
  WRITE_SHORT 0x8C ("%weap_idx%" + 1)
  WRITE_SHORT 0x84 (8 - "%weap_idx%")

COPY ~Divine_Remix/spl/cdslinf.spl~ ~override~  // infravision (permanent innate spell)

COPY ~Divine_Remix/spl/cdslmsd.spl~ ~override~  // Moon Shield (innate spell)
  SAY NAME1 @10307
  SAY UNIDENTIFIED_DESC @10308

COPY ~Divine_Remix/spl/trslmnb.spl~ ~override~  // Selune's Blade (innate spell)
  SAY NAME1 @10309
  SAY UNIDENTIFIED_DESC @10310

COPY ~Divine_Remix/eff/trslmnb.eff~ ~override~  // Selune's Blade
COPY ~Divine_Remix/itm/trslmnb.itm~ ~override~  // Selune's Blade

ADD_PROJECTILE ~Divine_Remix/spl/trslmnf.pro~ 	// New projectile
COPY ~Divine_Remix/spl/trslmnf.spl~ ~override~  // Moonfire (innate spell)
  SAY NAME1 @10311
  SAY UNIDENTIFIED_DESC @10312
  READ_LONG  0x64 ab_off 
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) ~%trslmnf%~        // use new projectile
  END
COPY ~Divine_Remix/bam/trslmnf.bam~ ~override~ // New Visual effect
	 ~Divine_Remix/bam/trslmnfa.bam~ ~override~
	 ~Divine_Remix/bam/trslmnfb.bam~ ~override~
	 ~Divine_Remix/bam/trslmnfc.bam~ ~override~
	 ~Divine_Remix/eff/trslmnfd.eff~ ~override~
	 ~Divine_Remix/eff/trslmnfi.eff~ ~override~

// change spells to innates as needed
COPY_EXISTING ~sppr505.spl~  ~override/cdsltrs.spl~ // True seeing (innate)
              ~spwi523.spl~  ~override/cdslsun.spl~ // sunfire (innate)
              ~spwi702.spl~  ~override/cdslpfe.spl~ // protection from the elements (innate)
  LAUNCH_PATCH_MACRO ~spell_to_innate~ // change to innate spell
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~spwi702.spl~ ~override~ // make innate/arcane PftE unstackable
  LPF CLONE_EFFECT STR_VAR match_resource = spwi702 resource = cdslpfe insert = above END // picks up 321 for ee games, 206 for non-ee

COPY_EXISTING ~cdslpfe.spl~ ~override~ // make innate/arcane PftE unstackable
  LPF CLONE_EFFECT STR_VAR match_resource = spwi702 resource = cdslpfe insert = below END // picks up 321 for ee games, 206 for non-ee
  
// conjure air elemental (innate)
ACTION_IF !((IDS_OF_SYMBOL (~spell~ ~CLERIC_CONJURE_AIR_ELEMENTAL~)) < 0) BEGIN // only if not present
  OUTER_SET airel = (IDS_OF_SYMBOL (~spell~ ~CLERIC_CONJURE_AIR_ELEMENTAL~) - 1000)

  COPY_EXISTING ~sppr%airel%.spl~ ~override/cdslair.spl~	
   LAUNCH_PATCH_MACRO ~spell_to_innate~ // change to innate spell
   BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN

  OUTER_SPRINT level_match  @2027 // needed to change descripts, below

  COPY_EXISTING ~sppr702.spl~ ~override/cdslair.spl~
  LAUNCH_PATCH_MACRO ~spell_to_innate~ // change to innate spell
	REPLACE_TEXTUALLY SPEART1P DRAIR1P (8)
    REPLACE_TEXTUALLY SPEART2P DRAIR2P (8)
    REPLACE_TEXTUALLY SPEART3P DRAIR3P (8)
    REPLACE_TEXTUALLY CASE_INSENSITIVE SPPR702B SPWI621B (8)
    REPLACE_TEXTUALLY CASE_INSENSITIVE SPPR702C SPWI621C (8)
	WRITE_LONG 0x8 24839
	WRITE_LONG 0x50 24840
    READ_LONG 0x50 desc_strref
    PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
      READ_STRREF 0x50 desc
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%level_match%~ ~\1 7~ // change level in description to 7
      END
    END
    SAY_EVALUATED 0x50 ~%desc%~

// Cretaing the new creatuers to remove the mental combat script for priest
  COPY_EXISTING "SPAIR1.EFF" "override/DRAIR1P.EFF" WRITE_ASCIIE 0x30 DRAIRPR2 (8)
  COPY_EXISTING "SPAIR2.EFF" "override/DRAIR2P.EFF" WRITE_ASCIIE 0x30 DRAIRPR3 (8)
  COPY_EXISTING "SPAIR3.EFF" "override/DRAIR3P.EFF" WRITE_ASCIIE 0x30 DRAIRPR4 (8)
  COPY_EXISTING "ELAIRSU2.CRE" "override/DRAIRPR2.cre"
	  WRITE_ASCII 0x248 "None" #8
  COPY_EXISTING "ELAIRSU3.CRE" "override/DRAIRPR3.cre"
	  WRITE_ASCII 0x248 "None" #8
  COPY_EXISTING "ELAIRSU4.CRE" "override/DRAIRPR4.cre"
	  WRITE_ASCII 0x248 "None" #8

END

